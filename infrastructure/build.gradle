buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath('org.flywaydb:flyway-mysql:8.5.13')
    }
}

plugins {
    id 'java'
    id 'application' // PERMITE AO GRADLE EXECUTAR O PROJETO COMO UM JAVA APP
    id 'org.springframework.boot' version '2.6.15'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'org.flywaydb.flyway' version '8.5.10'
}

group 'com.fullcycle.admin.catalogo.infrastructure'
version '1.0-SNAPSHOT'

bootJar {
    archiveFileName = 'application.jar'
    destinationDirectory.set(file("${rootProject.buildDir}/libs"))
}

repositories {
    mavenCentral()
}

dependencies {
    implementation(project(":domain"))
    implementation(project(":application"))

    implementation("com.google.cloud:google-cloud-storage:2.15.1")
    implementation("com.google.guava:guava:31.1-jre")

    implementation('org.springframework.boot:spring-boot-starter-web') {
        exclude module: 'spring-boot-starter-tomcat'
    }
    implementation('org.springframework.boot:spring-boot-starter-undertow')
    implementation('org.springframework.boot:spring-boot-starter-data-jpa')

    implementation('io.vavr:vavr:0.10.4')
    implementation('com.mysql:mysql-connector-j:8.3.0')

    implementation('com.fasterxml.jackson.module:jackson-module-afterburner')
    implementation('com.github.javafaker:javafaker:1.0.2')

    implementation('org.springdoc:springdoc-openapi-webmvc-core:1.6.8')
    implementation('org.springdoc:springdoc-openapi-ui:1.6.8')

    testImplementation(project(path: ':domain', configuration: 'testClasses'))

    testImplementation('com.github.javafaker:javafaker:1.0.2')
    testImplementation('org.flywaydb:flyway-core')
    testImplementation('org.springframework.boot:spring-boot-starter-test')

    testImplementation('org.testcontainers:testcontainers:1.17.6')
    testImplementation('org.testcontainers:mysql:1.17.6')
    testImplementation('org.testcontainers:junit-jupiter:1.17.6')

    testRuntimeOnly('com.h2database:h2')
}

flyway {
    url = System.getenv('FLYWAY_DB') ?: 'jdbc:mysql://localhost:3306/adm_videos'
    user = System.getenv('FLYWAY_USER') ?: 'root'
    password = System.getenv('FLYWAY_PASS') ?: '123456'
}

test {
    useJUnitPlatform()
}